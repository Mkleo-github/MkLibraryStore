apply plugin: 'com.android.library'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 19
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    // 忽略错误
    lintOptions {
        abortOnError false
    }

}

dependencies {
    implementation 'com.android.support:appcompat-v7:28.0.0'
}


//JCenter
//Gradle Android Maven plugin
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
//发布到组织名称名字，必须填写
group = "com.mkleo"
// 版本号，下次更新是只需要更改版本号即可
version = "3.0.1"
//发布到JCenter上的项目名字，必须填写
def libName = "MkGLES"

//生成源文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
//生成文档
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding "UTF-8"
    options.charSet 'UTF-8'
    options.author true
    options.version true
    failOnError false
}

//文档打包成jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
//上传到jcenter所需要的源码文件
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 配置maven库，生成POM.xml文件
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name 'MkGLES'
                description ''      //不能使用中文
                developers {
                    developer {
                        id 'Mkleo'
                        artifactId = 'gles'
                        name 'Wang HengJin'
                        email 'Mkleo627586724@outlook.com'
                    }
                }
            }
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {

    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']

    pkg {
        userOrg = "mkleo"
        repo = "maven"
        name = libName
        licenses = ["Apache-2.0"]
        publish = true
    }
}